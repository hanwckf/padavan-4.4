opengt
 set com 115200n81
 set senddelay 0.03
 waitquiet 1 0.2

 send "AT+CPIN?^m"
 waitfor 30 "SIM PIN","SIM PUK","READY","ERROR","ERR"
 if % = -1 goto simerror
 if % = 0 goto getpin
 if % = 1 goto ready
 if % = 2 goto ready
 goto error

:simerror
 print $s," ***SIM ERROR***\n"
 print "Check device port configuration.Check SIM is inserted\nTest SIM in a mobile phone?\n"
 send "ATH^m"
 exit 1

:getpin
 let $x=$rpipe("nvram get modem_pin")
 let a=len($x)
 if a=0 goto pinenverr
 if a<>4 goto pinerror
 let c=0

:test
 let $c=$mid($x,c,1)
 if $c<"0" goto pinerror
 if $c>"9" goto pinerror
 inc c
 if c<4 goto test
 let a=val($x)
 if a<0 goto pinerror
 if a>9999 goto pinerror
 let $c=$left($x,4)

:enterpin
 send "AT+CPIN=\""+$c+"\"^m"
 waitfor 20 "OK","ERR","ERROR","+CME ERROR"
 if % = -1 goto pintimeerror
 if % = 0 goto ready
 if % = 1 goto pinerror
 if % = 2 goto pinerror
 if % = 3 goto pinerror

:pinenverr
 print "ERROR: The COMGTPIN env variable is not set\n"
 send "ATH^m"
 exit 1

:pinerror
 print "ERROR: PIN code must be 4 decimal digits only\n"
 print "Caution! - entering the wrong PIN code three times will lock the SIM\n"
 send "ATH^m"
 exit 1

:pintimeerror
 print "ERROR: timeout, device did not respond to PIN command entry.\n"
 send "ATH^m"
 exit 1

:ready
 send "AT^m"
 waitfor 10 "OK","ERR","ERROR"
 if % = -1 goto timeerror
 if % = 0 goto next10
 if % = 1 goto error
 if % = 2 goto error

:next10
 send "AT+CFUN=1^m"
 waitfor 10 "OK","ERR","ERROR"
 if % = -1 goto timeerror
 if % = 0 goto next20
 if % = 1 goto error
 if % = 2 goto error

:next20
 send "ATS0=0^m"
 waitfor 10 "OK","ERR","ERROR"
 if % = -1 goto timeerror
 if % = 0 goto next30
 if % = 1 goto error
 if % = 2 goto error

:next30
 let $x=$rpipe("nvram get modem_cmd")
 let a=len($x)
 if a<3 goto next31

 send $x+"^m"
 waitfor 10 "OK","ERR","ERROR"
 if % = -1 goto timeerror

:next31
 let $x=$rpipe("nvram get modem_apn")
 let a=len($x)
 if a=0 goto apnerror
 if a>32 goto apnerror

 send "AT+CGDCONT=1,\"IP\",\""+$x+"\"^m"
 waitfor 10 "OK","ERR","ERROR"
 if % = -1 goto apntimeerror
 if % = 0 goto next40
 if % = 1 goto apnerror
 if % = 2 goto apnerror

:next40
 let $x=$rpipe("nvram get modem_dialnum")
 let a=len($x)
 if a=0 goto apnerror

 send "ATD"+$x+"^m"
 waitfor 10 "CONNECT","ERR","ERROR"
 if % = -1 goto timeerror
 if % = 0 goto next50
 if % = 1 goto error
 if % = 2 goto error

:next50
 print "CONNECTED\n"
 exit 0

:apnerror
 print "ERROR entering APN\n"
 send "ATH^m"
 exit 1

:apntimeerror
 print \"ERROR entering APN, device timeout\n"
 send "ATH^m"
 exit 1

:error
 print "CONNECT ERROR\n"
 send "ATH^m"
 exit 1

:timeerror
 print "CONNECT TIMEOUT\n"
 send "ATH^m"
 exit 1

