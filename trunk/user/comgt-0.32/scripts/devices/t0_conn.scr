opengt
 set com 460800n81
 set senddelay 0.03
 waitquiet 1 0.2

 send "ATZ^m"
 waitfor 10 "OK","ERR"
 if % = -1 goto timeerror
 if % = 0 goto OK1
 if % = 1 goto error

:OK1
 send "E1 Q0 V1 X4 &C1 &D1 &R1 &S0 &K0 +FCLASS0^m"
 waitfor 10 "OK","ERR"
 if % = -1 goto timeerror
 if % = 0 goto OK2
 if % = 1 goto error

:OK2
 let $x=$rpipe("nvram get modem_cmd")
 let a=len($x)
 if a<3 goto OK3

 send $x+"^m"
 waitfor 10 "OK","ERR","ERROR"
 if % = -1 goto timeerror

:OK3
 let $x=$rpipe("nvram get modem_apn")
 let a=len($x)
 if a=0 goto error
 if a>32 goto error

 send "AT+CGDCONT=1,\"IP\",\""+$x+"\"^m"
 waitfor 20 "OK","ERR"
 if % = -1 goto timeerror
 if % = 0 goto OK
 if % = 1 goto error

:OK
 let $x=$rpipe("nvram get modem_dialnum")
 let a=len($x)
 if a=0 goto error

 send "ATDT"+$x+"^m"
 waitfor 10 "CONNECT","ERR","ERROR"
 if % = -1 goto timeerror
 if % = 0 goto next3
 if % = 1 goto error
 if % = 2 goto error

:next3
 print "CONNECTED\n"
 exit 0

:error
 print "CONNECT ERROR\n"
 send "ATH^m"
 exit 1

:timeerror
 print "CONNECT TIMEOUT\n"
 send "ATH^m"
 exit 1

