opengt
 set com 115200n81
 set senddelay 0.03
 waitquiet 1 0.2

 let $x=$rpipe("nvram get modem_cmd")
 let a=len($x)
 if a<3 goto ready

 send $x+"^m"
 waitfor 10 "OK","ERR","ERROR","+CME ERROR","COMMAND NOT SUPPORT"
 if % = 0 goto ready
 let $e="ERROR sending user AT command! Try to continue..."
 print $e+"\n"
 system "logger -t comgt \""+$e+"\""

:ready
 let $x=$rpipe("nvram get modem_apn")
 send "AT\^NDISDUP=1,1,\""+$x+"\"^m"
 waitfor 5 "OK","ERR","ERROR","+CME ERROR","COMMAND NOT SUPPORT"
 if % = -1 goto timeerror
 if % = 0 goto dialok
 goto apnerror

:dialok
 let $e="NCM CONNECTION ESTABLISHED!"
 print $e+"\n"
 system "logger -t comgt \""+$e+"\""
 exit 0

:apnerror
 let $e="ERROR: NDISDUP ERROR!"
 print $e+"\n"
 system "logger -t comgt \""+$e+"\""
 exit 1

:timeerror
 let $e="ERROR: CONNECT TIMEOUT!"
 print $e+"\n"
 system "logger -t comgt \""+$e+"\""
 exit 1

