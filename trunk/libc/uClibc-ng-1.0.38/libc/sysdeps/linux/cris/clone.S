/* Copyright (C) 2001, 2003 Free Software Foundation, Inc.
   This file is part of the GNU C Library.

   The GNU C Library is free software; you can redistribute it and/or
   modify it under the terms of the GNU Library General Public License as
   published by the Free Software Foundation; either version 2 of the
   License, or (at your option) any later version.

   The GNU C Library is distributed in the hope that it will be useful,
   but WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
   Library General Public License for more details.

   You should have received a copy of the GNU Library General Public
   License along with the GNU C Library; see the file COPYING.LIB.  If not,
   see <http://www.gnu.org/licenses/>.  */

#include <features.h>
#include <sys/syscall.h>
#include "sysdep.h"
#define _ERRNO_H	1
#include <bits/errno.h>

/* int clone(int (*fn)(void *arg), void *child_stack, int flags, void *arg); */

	.syntax no_register_prefix

        .text
ENTRY (clone)
	/* Sanity check arguments: No NULL function pointers.  Allow a NULL
	   stack pointer though; it makes the kernel allocate stack.  */
	cmpq	0,r10
	beq	1f
	nop

	/* We need to muck with a few registers.  */
	subq	8,sp
	movem	r1,[sp]

	/* Save the function pointer and argument.  We can't save them
	   onto the new stack since it can be NULL.  */
	move.d	r10,r0
	move.d	r13,r1

        /* Move the other arguments into place for the system call.  */
	move.d	r11,r10
	move.d	r12,r11

	/* Do the system call.  */
	movu.w	SYS_ify (clone),r9
	break	13
	cmpq	0,r10
	beq	.Lthread_start
	nop

	/* Jump to error handler if we get (unsigned) -4096 .. 0xffffffff.  */
	cmps.w	-4096,r10
	bhs	0f
	movem	[sp+],r1

	/* In parent, successful return.  (Avoid using "ret" - it's a macro.)  */
	Ret
	nop

.Lthread_start:
	/* Terminate frame pointers here.  */
	moveq	0,r8

#ifdef __arch_v32
	/* Is this the right place for an argument?  */
	jsr	r0
	move.d	r1,r10
#else
	/* I've told you once.  */
	move.d	r1,r10
	jsr	r0
#endif

	SETUP_PIC
	PLTCALL	(HIDDEN_JUMPTARGET(_exit))

	/* Die horribly.  */
	move.d 6809,r13
	test.d	[r13]

	/* Stop the unstoppable.  */
9:
	ba	9b
	nop

/* Local error handler.  */
1:
	movs.w	-EINVAL,r10
	/* Drop through into the ordinary error handler.  */
PSEUDO_END (clone)
